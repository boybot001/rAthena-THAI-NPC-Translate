//===== rAthena Script =======================================
//= Reset NPC
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Resets skills, stats, or both.
//===== Additional Comments: =================================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//= 1.3 All statuses removed upon skill reset. [Euphy]
//= 1.4 Compressed Script, Added limit use option [Stolao]
//=	Changed set -> setarray, Improved text with F_InsertPlural
//============================================================
prontera,150,193,4	script	พนักงานรีเซ็ท	124,{
	//	 		Skills,	Stats,	Both,	Limit
	setarray .@Reset,	5000,	5000,	9000,	0;
	mes "[พนักงานรีเซ็ท]";
	if(.@Reset[3] && reset_limit > .@Reset[3]) {
		mes "ขอโทษนะ ท่านสามารถรีเซ็ทได้แค่ "+callfunc("F_InsertPlural",.@Reset[3],"ครั้ง")+" ในชีวิตท่าน";
		close;
	}
	mes "ข้าคือพนักงานรีเซ็ท";
	mes "รีเซ็ท Stats: "+ callfunc("F_InsertComma",.@Reset[1]) +"z";
	mes "รีเซ็ท Skills: "+ callfunc("F_InsertComma",.@Reset[0]) +"z";
	mes "รีเซ็ททั้งคู่: "+ callfunc("F_InsertComma",.@Reset[2]) +"z";
	if(.@Reset[3]) mes "ท่านสามารถรีเซ็ทได้แค่ "+callfunc("F_InsertPlural",.@Reset[3],"ครั้ง")+", ดังนั้นใช้ "+((.@Reset[3]>1)?"พวกมัน":"มัน")+" ให้คุ้ม";
	mes "โปรดเลือกบริการที่ท่านต้องการ:";
	next;
	set .@i,(select("^FF3355รีเซ็ท Skills:รีเซ็ท Stats:รีเซ็ททั้งคู่^000000:ยกเลิก"));
	if(.@i > 3) close;
	mes "[พนักงานรีเซ็ท]";
	if (Zeny < .@Reset[.@i-1]) {
		mes "ขอโทษนะ แต่ท่านมีเงินไม่พอ";
		close;
	}
	if(.@Reset[3]){
		mes "ท่านสามารถรีเซ็ทได้แค่ "+callfunc("F_InsertPlural",.@Reset[3],"ครั้ง")+" ในชีวิตท่าน แน่ใจรึยัง?";
		if(select("ขอคิดดูก่อน:ไม่เป็นไร เริ่มได้เลย") == 1) close;
	}
	set Zeny, Zeny-.@Reset[.@i-1];
	if(.@i&1){
		sc_end SC_ALL;// TODO make a sc_end current classes sc only
		ResetSkill;
	}
	if(.@i&2) ResetStatus;
	mes "เรียบร้อย!";
	if(.@Reset[3]) set reset_limit,reset_limit + 1;
	close;
}
